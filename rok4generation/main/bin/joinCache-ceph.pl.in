#!/usr/bin/env perl
# Copyright © (2011) Institut national de l'information
#                    géographique et forestière
#
# Géoportail SAV <geop_services@geoportail.fr>
#
# This software is a computer program whose purpose is to publish geographic
# data using OGC WMS and WMTS protocol.
#
# This software is governed by the CeCILL-C license under French law and
# abiding by the rules of distribution of free software.  You can  use,
# modify and/ or redistribute the software under the terms of the CeCILL-C
# license as circulated by CEA, CNRS and INRIA at the following URL
# "http://www.cecill.info".
#
# As a counterpart to the access to the source code and  rights to copy,
# modify and redistribute granted by the license, users are provided only
# with a limited warranty  and the software's author,  the holder of the
# economic rights,  and the successive licensors  have only  limited
# liability.
#
# In this respect, the user's attention is drawn to the risks associated
# with loading,  using,  modifying and/or developing or reproducing the
# software by the user in light of its specific status of free software,
# that may mean  that it is complicated to manipulate,  and  that  also
# therefore means  that it is reserved for developers  and  experienced
# professionals having in-depth computer knowledge. Users are therefore
# encouraged to load and test the software's suitability as regards their
# requirements in conditions enabling the security of their systems and/or
# data to be ensured and,  more generally, to use and operate it in the
# same conditions as regards security.
#
# The fact that you are presently reading this means that you have had
#
# knowledge of the CeCILL-C license and that you accept its terms.

################################################################################

=begin nd
File: joinCache-ceph.pl
=cut

################################################################################

use warnings;
use strict;

use POSIX qw(locale_h);

use Getopt::Long;

use Data::Dumper;
local $Data::Dumper::Maxdepth = 2;
use Math::BigFloat;
use File::Basename;
use File::Spec;
use File::Path;
use Cwd;

use Log::Log4perl qw(:easy);

# My search module
use FindBin qw($Bin);
use lib "$Bin/../lib/perl5";

# My module
use COMMON::TileMatrixSet;
use COMMON::TileMatrix;
use COMMON::PyramidRaster;
use COMMON::LevelRaster;
use COMMON::Script;

use JOINCACHE::Node;
use JOINCACHE::Shell;
use JOINCACHE::PropertiesLoader;


################################################################################
# Constantes
use constant TRUE  => 1;
use constant FALSE => 0;

################################################################################
# Pas de bufferisation des sorties.
$|=1;

####################################################################################################
#                                       Group: Variables                                           #
####################################################################################################

# Variable: version
my $VERSION = '@VERSION_TEXT@';

=begin nd
Variable: options

Contains joinCache call options :

    version - To obtain the command's version
    help - To obtain the command's help
    usage - To obtain the command's usage
    properties - Configuration file
=cut
my %options =
(
    "version" => 0,
    "help" => 0,
    "usage" => 0,

    # Configuration
    "properties" => undef, # file properties params (mandatory) !
);

=begin nd
Variable: this

Informations are treated, interpreted and store in this hash, using JOINCACHE classes :

    propertiesLoader - <JOINCACHE::PropertiesLoader> - Contains all raw informations
    pyramid - <COMMON::PyramidRaster> - Output pyramid
    mergeMethod - string - Method to use merge slabs when several sources
    useMasks - boolean - do we use masks to generate slabs
    scripts - <COMMON::Script> array - Split scripts to use to generate slabs to merge
    currentScript - integer - Script index to use for the next task (round robin share)
    composition - hash - Defines source pyramids for each level, extent, and order
|       level_id => [
|           { extent => OGR::Geometry, bboxes => [[bbox1], [bbox2]], pyr => COMMON::PyramidRaster}
|           { extent => OGR::Geometry, bboxes => [[bbox1], [bbox2]], pyr => COMMON::PyramidRaster}
|       ]
    listStream - file stream - Open file stream to write the new pyramid list
    doneSlabs - boolean hash - To memorize already done tiles, we use this hash, containing "I_J => TRUE".
=cut
my %this =
(
    propertiesLoader => undef,

    pyramid => undef,

    mergeMethod => undef,
    useMasks => undef,
    scripts => [],
    currentScript => undef,

    composition => undef,

    listStream => undef,
    roots => {},

    doneSlabs => {}
);

####################################################################################################
#                                         Group: Functions                                         #
####################################################################################################

=begin nd
Function: main

Main method.

See Also:
    <init>, <config>, <validate>, <doIt>
=cut
sub main {
    printf("JOINCACHE : version [%s]\n",$VERSION);

    print STDOUT "BEGIN\n";

    # initialization
    ALWAYS("> Initialization");
    if (! main::init()) {
        print STDERR "ERROR INITIALIZATION !\n";
        exit 1;
    }

    # configuration
    ALWAYS("> Configuration");
    if (! main::config()) {
        print STDERR "ERROR CONFIGURATION !\n";
        exit 2;
    }

    # execution
    ALWAYS("> Validation");
    if (! main::validate()) {
        print STDERR "ERROR VALIDATION !\n";
        exit 3;
    }

    # execution
    ALWAYS("> Execution");
    if (! main::doIt()) {
        print STDERR "ERROR EXECUTION !\n";
        exit 5;
    }

    print STDOUT "END\n";
}

=begin nd
Function: init

Checks options and initializes the default logger. Check properties file (mandatory).
=cut
sub init {

    ALWAYS(">>> Check Configuration ...");

    # init Getopt
    local $ENV{POSIXLY_CORRECT} = 1;

    Getopt::Long::config qw(
        default
        no_autoabbrev
        no_getopt_compat
        require_order
        bundling
        no_ignorecase
        permute
    );

    # init Options
    GetOptions(
        "help|h" => sub {
            printf "See documentation here: http://www.rok4.org/data/doc/pyramide/Html/files/bin/joinCache-pl.html\n" ;
            exit 0;
        },
        "version|v" => sub { exit 0; },
        "usage" => sub {
            printf "See documentation here: http://www.rok4.org/data/doc/pyramide/Html/files/bin/joinCache-pl.html\n" ;
            exit 0;
        },
        
        "properties|conf=s" => \$options{properties},     
    ) or do {
        printf "Unappropriate usage\n";
        printf "See documentation here: http://www.rok4.org/data/doc/pyramide/Html/files/bin/joinCache-pl.html\n";
        exit -1;
    };
  
    # logger by default at runtime
    Log::Log4perl->easy_init({
        level => "WARN",
        layout => '%5p : %m (%M) %n'
    });

    # We make path absolute

    # properties : mandatory !
    if (! defined $options{properties} || $options{properties} eq "") {
        FATAL("Option 'properties' not defined !");
        return FALSE;
    }
    my $fproperties = File::Spec->rel2abs($options{properties});
    $options{properties} = $fproperties;

    return TRUE;
}

=begin nd
Function: config

Load all parameters from the configuration file, using <JOINCACHE::PropertiesLoader>.

See Also:
    <checkParams>
=cut
sub config {

    ALWAYS(">>> Load Properties ...");

    my $objProp = JOINCACHE::PropertiesLoader->new($options{properties});
    
    if (! defined $objProp) {
        FATAL("Can not load specific properties !");
        return FALSE;
    }

    $this{propertiesLoader} = $objProp;

    ###################

    my $logger = $this{propertiesLoader}->getLoggerSection();
    
    # logger
    if (defined $logger) {
        my @args;

        my $layout= '%5p : %m (%M) %n';
        my $level = $logger->{log_level};

        my $out   = "STDOUT";
        $level = "WARN"   if (! defined $level);

        if ($level =~ /(ALL|DEBUG)/) {
            $layout = '%5p : %m (%M) %n';
        }

        # add the param logger by default (user settings !)
        push @args, {
            file   => $out,
            level  => $level,
            layout => $layout,
        };

        Log::Log4perl->easy_init(@args);
    }

    # Environment variables

    if (! COMMON::ProxyStorage::checkEnvironmentVariables("CEPH")) {
        ERROR("Environment variable is missing for a CEPH storage");
        return FALSE;
    }

    return TRUE;
}

####################################################################################################
#                                 Group: Validation methods                                        #
####################################################################################################

=begin nd
Function: validate

Validates all components, checks consistency and create scripts. Use classes <COMMON::PyramidRaster>, <COMMON::Script>

See Also:
    <validateSourcePyramids>
=cut
sub validate {

    ##################

    ALWAYS(">>> Create the output COMMON::PyramidRaster object ...");

    my $pyramidSection = $this{propertiesLoader}->getPyramidSection();

    my $objPyramid = COMMON::PyramidRaster->new("VALUES", $pyramidSection);

    if (! defined $objPyramid) {
        ERROR ("Cannot create the COMMON::PyramidRaster object for the output pyramid !");
        return FALSE;
    }

    if ($objPyramid->getStorageType() ne "CEPH") {
        ERROR ("JoinCache-Ceph only handle CEPH output pyramid");
        return FALSE;
    }
    
    if (! $objPyramid->bindTileMatrixSet($pyramidSection->{tms_path})) {
        ERROR("Cannot bind TMS to output pyramid");
        return FALSE;
    }

    $this{pyramid} = $objPyramid;

    ################## Process

    ALWAYS(">>> Create the COMMON::Script objects ...");

    my $processSection = $this{propertiesLoader}->getProcessSection();

    $this{mergeMethod} = $processSection->{merge_method};

    if (! JOINCACHE::Shell::setGlobals($processSection->{path_temp_common}, $processSection->{merge_method}, $processSection->{use_masks})) {
        ERROR ("Impossible d'initialiser la librairie des commandes Shell pour JOINCACHE");
        return FALSE;
    }
    my $scriptInit = JOINCACHE::Shell::getScriptInitialization($this{pyramid});

    for (my $i = 1; $i <= $processSection->{job_number}; $i++ ) {
        my $script = COMMON::Script->new({
            id => "SCRIPT_$i",
            tempDir => $processSection->{path_temp},
            scriptDir => $processSection->{path_shell},
            executedAlone => FALSE,
            initialisation => $scriptInit
        });

        if (! defined $script) {
            ERROR ("Cannot create the COMMON::Script object !");
            return FALSE;
        }

        push(@{$this{scripts}}, $script);
    }
    $this{currentScript} = 0;

    ##################

    ALWAYS(">>> Validate source pyramids ...");

    if (! main::validateSourcePyramids($pyramidSection->{tms_path})) {
        ERROR ("Some source pyramids are not valid !");
        return FALSE;
    }

    ##################

    $this{composition} = $this{propertiesLoader}->getCompositionSection();

    return TRUE;

}

=begin nd
Function: validateSourcePyramids

For each source pyramid (<COMMON::PyramidRaster>), we bind the TMS (<COMMON::PyramidRaster::bindTileMatrixSet>) and we check its compatibility with the output pyramid (<COMMON::PyramidRaster::checkCompatibility>)
=cut
sub validateSourcePyramids {
    my $tms_path = shift;

    my $sourcePyramids = $this{propertiesLoader}->getSourcePyramids();

    foreach my $sourcePyramid (values %{$sourcePyramids}) {

        if (! $sourcePyramid->bindTileMatrixSet($tms_path)) {
            ERROR("Cannot bind TMS to source pyramid " . $sourcePyramid->getName());
            return FALSE;
        }

        if ($sourcePyramid->checkCompatibility($this{pyramid}) == 0) {
            ERROR (sprintf "Source pyramid (%s) and output pyramid are not compatible", $sourcePyramid->getName());
            return FALSE;
        }

        if ($sourcePyramid->getDataPool() ne $this{pyramid}->getDataPool() ) {
            ERROR (sprintf "Source pyramid (%s) and output pyramid are not on the same pool", $sourcePyramid->getName());
            return FALSE;
        }

        if (! $sourcePyramid->loadList()) {
            ERROR("Cannot cache content list for source pyramid " . $sourcePyramid->getName());
            return FALSE;
        }

        INFO($sourcePyramid->getName());
        INFO($sourcePyramid->getCachedListStats());
    }

    return TRUE;
}

####################################################################################################
#                                 Group: Process methods                                           #
####################################################################################################

=begin nd
Function: doIt

We browse all source pyramids to identify images to generate. 

For each level, for each source pyramid :
    - Identify present images in the extent
    - Work has already been made ? Next
    - Else search it in the other source pyramids with an lower priority
    - Treat source(s) : <main::treatNode>

=cut
sub doIt {

    ALWAYS(">>> Browse source pyramids");

    my $pyramid = $this{pyramid};
    my $TPW = $pyramid->getTilesPerWidth();
    my $TPH = $pyramid->getTilesPerHeight();
    my $TMS = $pyramid->getTileMatrixSet();

    my $listFile = $pyramid->getListFile();
    my $dir = File::Basename::dirname($listFile);

    if (! -d $dir) {
        eval { mkpath([$dir]); };
        if ($@) {
            ERROR(sprintf "Can not create the output pyramid list directory '%s' : %s !", $dir , $@);
            return FALSE;
        }
    }

    my $STREAM;
    if (! open $STREAM, ">", $listFile) {
        ERROR(sprintf "Cannot open output pyramid list file (write) : %s",$listFile);
        return FALSE;
    }
    $this{listStream} = $STREAM;

    printf $STREAM "0=%s\n#\n", $pyramid->getDataPool();

    while( my ($level,$sources) = each(%{$this{composition}}) ) {
        INFO(sprintf "Level %s",$level);

        if (! $pyramid->addLevel($level)) {
            ERROR("Cannot add level $level to output pyramid");
            return FALSE;
        }

        my $tm = $pyramid->getTileMatrixSet()->getTileMatrix($level);

        # On fait une première passe sur les sources pour calculer les indices extrêmes des dalles pour ce niveau pour l'extent de la source
        foreach my $source (@{$sources}) {
            my @a = $source->{pyr}->getLevel($level)->bboxToSlabIndices(@{$source->{bbox}});
            $source->{extrem_slabs} = \@a;
        }

        my $nSource = 0;
        foreach my $source (@{$sources}) {

            # On va boucler sur toutes les dalles de ce niveau dans cette source, et on vérifiera qu'elle appartient bien à l'extent

            my $slabsHash = $source->{pyr}->getLevelSlabs($level);
            # Hash de la forme { IMAGE => {COL_ROW => pool/slab_object_name} }

            while (my ($slabKey, $slabPath) = each(%{$slabsHash->{IMAGE}})) {

                if (exists $this{doneSlabs}->{$slabKey}) {
                    # Image already treated
                    next;
                }

                my ($COL, $ROW) = split(/_/, $slabKey);

                my $node = JOINCACHE::Node->new({
                    level => $level,
                    col => $COL,
                    row => $ROW,
                    pyramid => $pyramid,
                    sourcePyramids => $sources,
                    mainSourceIndice => $nSource
                });

                if (! defined $node) {
                    ERROR(sprintf "Cannot create the node %s,%s", $COL, $ROW);
                    return FALSE;
                }

                if ($node->getSourcesNumber() == 0) {
                    # Pas de source pour cette image, pour cette priorité
                    next;
                }

                if (! main::treatNode($node)) {
                    ERROR(sprintf "Cannot generate the node %s", Dumper($node));
                    return FALSE;
                }

                $pyramid->getLevel($level)->updateLimitsFromSlab($COL, $ROW);
                $this{doneSlabs}->{$slabKey} = TRUE;

            }

            $nSource++;
        }

        delete $this{doneSlabs};
    }

    close($STREAM);

    $this{pyramid}->backupList();

    # On ferme tous les scripts
    foreach my $s (@{$this{scripts}}) {
        $s->close();
    }

    # writting pyramid's configuration file
    ALWAYS(">>> Write pyramid's descriptor");
    if (! $this{pyramid}->writeDescriptor()) {
        ERROR("Can not write Pyramid file !");
        return FALSE;
    }

    # Écrire le script principal
    ALWAYS(">>> Write main script");
    my $scriptPath = File::Spec->catfile($this{propertiesLoader}->getProcessSection()->{path_shell}, "main.sh");
    open(MAIN, ">$scriptPath") or do {
        ERROR("Cannot open '$scriptPath' to write in it");
        return FALSE;
    };

    print MAIN JOINCACHE::Shell::getMainScript($this{propertiesLoader}->getProcessSection()->{path_shell}, $this{propertiesLoader}->getProcessSection()->{job_number});

    close(MAIN);

    INFO("To run all scripts on the same machine, run :");
    INFO("\t bash $scriptPath");

    return TRUE;
}

sub treatNode {
    my $node = shift;

    my $finalImage = $node->getSlabPath("IMAGE", TRUE);

    my $finalMask = undef;
    if ($this{pyramid}->ownMasks()) {
        $finalMask = $node->getSlabPath("MASK", TRUE);
    }

    # On affecte le script courant au noeud. Le script courant n'est pas incrémenté dans le cas d'un lien, car rien n'est écrit dans le script.
    $node->setScript($this{scripts}->[$this{currentScript}]);

    if ($node->getSourcesNumber() == 1) {

        my $sourceImage = $node->getSource(0);

        if ($sourceImage->{compatible}) {
            # We can just make a symbolic link, in a script
            $node->linkSlab($sourceImage->{img}, $finalImage);

            main::storeInList($sourceImage->{img});
            
        } else {
            # We have just one source image, but it is not compatible with the final cache
            # We need to transform it (pull push).
            if ( ! $node->convert() ) {
                ERROR(sprintf "Cannot transform the image");
                return FALSE;
            }

            main::storeInList($finalImage);
        }

        # Export éventuel du masque : il est forcément dans le bon format, on peut donc le lier.
        if (exists $sourceImage->{msk} && defined $finalMask) {
            # We can just make a symbolic link, in a script
            $node->linkSlab($sourceImage->{msk}, $finalMask);

            main::storeInList($sourceImage->{msk});
        }
        
    } else {
        # We have several images, we merge them
        if ( ! $node->overlayNtiff() ) {
            ERROR(sprintf "Cannot merge the images");
            return FALSE;
        }

        main::storeInList($finalImage);

        if (defined $finalMask) {
            main::storeInList($finalMask);
        }
    }
    
    $this{currentScript} = ($this{currentScript}+1)%(scalar(@{$this{scripts}}));

    return TRUE;
}

####################################################################################################
#                                   Group: List methods                                            #
####################################################################################################

=begin nd
Function: storeInList

Writes slab's path in the pyramid's content list and store the root.

Parameters:
    slabPathEnd - string - End of the slab path (without root).
    slabPath - string - Full slab path, to store in the file list (with indexed root).
=cut
sub storeInList {
    my $slabPath = shift;

    my $poolName = $this{pyramid}->getDataPool();
    $slabPath =~ s/$poolName/0/;

    my $STREAM = $this{listStream};
    print $STREAM "$slabPath\n";
}

################################################################################

BEGIN {}
INIT {}

main;
exit 0;

END {}

################################################################################

1;
__END__
